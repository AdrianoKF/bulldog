def javaProjects = [
					project(":bulldog.core"), 
					project(":bulldog.devices"), 
					project(":bulldog.examples"), 
					project(":bulldog.linux"),
				   ]
				   
def boardProjects = [
					 project(":bulldog.board.beagleboneblack"),
					 project(":bulldog.board.raspberrypi")
					]

def nativeProjects = [
					  project(":bulldog.linux.native")
					 ]
					 
def allJavaProjects = new LinkedList(javaProjects);
allJavaProjects.addAll(boardProjects)
		
if(hasProperty('board')) {
	boardProjects = boardProjects.findAll { it.path.contains(board) };
} 

def bulldogVersion = '0.1'

configure(nativeProjects) {
	apply from: new File(project.projectDir, 'build.gradle')
	version = bulldogVersion
}
					 
configure(allJavaProjects) { 
   	   
    apply plugin: 'java'
    apply plugin: 'eclipse'
   	
    version = bulldogVersion
 
    repositories {
       mavenCentral()
    }
 
    dependencies {
    	testCompile 'junit:junit:4.8.2'
    }
 
    jar {
        manifest.attributes 'Implementation-Version': version
        manifest.attributes provider: 'Datenheld'
        from ("${projectDir}/src/main/java/") {
        	include('*/**')
    	}
    }
    
    uploadArchives {
    	repositories {
	    	 flatDir {
	        	dirs 'bulldog.repository'
	    	}
    	}
    }	
}

boardProjects.each { def boardProject -> 
	evaluationDependsOn(boardProject.path) 
    tasks.create(name: "singleJar$boardProject.name", type: Jar) { 
        baseName = boardProject.distributionBaseName
        destinationDir = boardProject.distributionDirectory
        
        manifest { 
     		attributes 'Main-Class': 'org.bulldog.examples.App' 
     	}
    
    	javaProjects.each { javaProject -> 
    		evaluationDependsOn(javaProject.path) 
    		from zipTree(javaProject.jar.archivePath) 
		}	
		
		from zipTree(boardProject.jar.archivePath)
    } 
} 

boardProjects.each { def boardProject -> 
	evaluationDependsOn(boardProject.path)
	evaluationDependsOn(project(':bulldog.linux.native').path)
    tasks.create(name: "distribute$boardProject.name", type: Copy, dependsOn: [ project(':bulldog.linux.native').tasks.matching  { Task task -> task.name.contains('BulldogSharedLibrary') } ]) { 
       //delete project.distributionDirectory
       boardProject.distributionDirectory.mkdirs()
       FileCollection collection = boardProject.distributionFiles
       from collection
       into boardProject.distributionDirectory
    } 
} 

boardProjects.each { def boardProject -> 
	evaluationDependsOn(boardProject.path)
    tasks.create(name: "createZipArchive$boardProject.name", type: Zip) { 
       baseName = boardProject.distributionBaseName + '-' + boardProject.version	
       destinationDir = boardProject.distributionDirectory
       excludes = [ '*.zip' ]
       from boardProject.distributionDirectory
    } 
} 


task packageBoardArtifacts(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("singleJar")}, subprojects.assemble]) 
task distribution(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("distribute")}, packageBoardArtifacts])
task archiveDistributions(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("createZipArchive")}, distribution])







