def javaProjects = [
					project(":bulldog.core"), 
					project(":bulldog.devices"), 
					project(":bulldog.examples"), 
					project(":bulldog.beagleboneblack")
				   ]

def nativeProjects = [
						project(":bulldog.beagleboneblack.platform")
					 ]



def bulldogVersion = '0.1'


configure(nativeProjects) {
	apply from: new File(project.projectDir, 'build.gradle')
	version = bulldogVersion
}
					 
configure(javaProjects) { 
   	   
    apply plugin: 'java'
    apply plugin: 'eclipse'
   	
    version = bulldogVersion
 
    repositories {
       mavenCentral()
    }
 
    dependencies {
    	testCompile 'junit:junit:4.8.2'
    }
 
    jar {
        manifest.attributes 'Implementation-Version': version
        manifest.attributes provider: 'Datenheld'
    }
    
    uploadArchives {
    	repositories {
	    	 flatDir {
	        	dirs 'bulldog.repository'
	    	}
    	}
    }	
}

task bbbSingleJar(type: Jar, dependsOn: subprojects.assemble) { 
    baseName = 'bulldog-beagleboneblack' 
     manifest { 
     	attributes 'Main-Class': 'org.bulldog.examples.App' 
     }
    
    javaProjects.each { project -> 
    	evaluationDependsOn(project.path) 
    	from project.configurations.default.allArtifacts.files.collect { zipTree(it) } 
	}	
	 
}
