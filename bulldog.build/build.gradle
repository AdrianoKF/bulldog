def javaProjects = [
					project(":bulldog.core"), 
					project(":bulldog.devices"), 
					project(":bulldog.examples"), 
					project(":bulldog.linux"),
				   ]
				   
def boardProjects = [
					 project(":bulldog.board.beagleboneblack"),
					 project(":bulldog.board.raspberrypi")
					]

def nativeProjects = [
					  project(":bulldog.linux.native")
					 ]
					 
def allJavaProjects = new LinkedList(javaProjects);
allJavaProjects.addAll(boardProjects)

def bulldogVersion = '0.1'

configure(nativeProjects) {
	apply from: new File(project.projectDir, 'build.gradle')
	version = bulldogVersion
}
					 
configure(allJavaProjects) { 
   	   
    apply plugin: 'java'
    apply plugin: 'eclipse'
   	
    version = bulldogVersion
 
    repositories {
       mavenCentral()
    }
 
    dependencies {
    	testCompile 'junit:junit:4.8.2'
    }
 
    jar {
        manifest.attributes 'Implementation-Version': version
        manifest.attributes provider: 'Datenheld'
        from ("${projectDir}/src/main/java/") {
        	include('*/**')
    	}
    }
    
    uploadArchives {
    	repositories {
	    	 flatDir {
	        	dirs 'bulldog.repository'
	    	}
    	}
    }	
}

boardProjects.each { def project -> 
	evaluationDependsOn(project.path) 
    tasks.create(name: "singleJar$project.name", type: Jar) { 
        baseName = project.distributionBaseName
        destinationDir = project.distributionDirectory
        
        manifest { 
     		attributes 'Main-Class': 'org.bulldog.examples.AdcExample' 
     	}
    
    	javaProjects.each { javaProject -> 
    		evaluationDependsOn(javaProject.path) 
    		from zipTree(javaProject.jar.archivePath) 
		}	
		
		from zipTree(project.jar.archivePath)
    } 
} 

boardProjects.each { def project -> 
	evaluationDependsOn(project.path)
    tasks.create(name: "distribute$project.name", type: Copy) { 
       delete project.distributionDirectory
       project.distributionDirectory.mkdirs()
       FileCollection collection = project.distributionFiles
       from collection
       into project.distributionDirectory
    } 
} 

boardProjects.each { def project -> 
	evaluationDependsOn(project.path)
    tasks.create(name: "createZipArchive$project.name", type: Zip) { 
       baseName = project.distributionBaseName + '-' + project.version	
       destinationDir = project.distributionDirectory
       excludes = [ '*.zip' ]
       from project.distributionDirectory
    } 
} 

task packageBoardArtifacts(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("singleJar")}, subprojects.assemble]) 
task distribution(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("distribute")}, packageBoardArtifacts])
task archiveDistributions(dependsOn: [ tasks.matching { Task task -> task.name.startsWith("createZipArchive")}, distribution])







